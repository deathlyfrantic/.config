fpath=(/usr/local/share/zsh-completions/ $fpath)

# colors are important here in the future, where we live
autoload colors; colors

typeset -A fg_bright
if [[ $TERM == 'tmux-256color' || -n "$COLORTERM" || -n $VIMHOME || $TERM_PROGRAM == "Apple_Terminal" ]]; then
    fg_bright+=(black   $(echo -e '\e[90m'))
    fg_bright+=(red     $(echo -e '\e[91m'))
    fg_bright+=(green   $(echo -e '\e[92m'))
    fg_bright+=(yellow  $(echo -e '\e[93m'))
    fg_bright+=(blue    $(echo -e '\e[94m'))
    fg_bright+=(magenta $(echo -e '\e[95m'))
    fg_bright+=(cyan    $(echo -e '\e[96m'))
else
    set -A fg_bright ${(kv)fg_bold}
fi

# general options
setopt appendhistory autocd extended_history share_history menu_complete prompt_subst
unsetopt beep case_glob flowcontrol

# prompt
set-up-prompt

# history
HISTFILE="$XDG_DATA_HOME"/zsh-history
HISTSIZE=1000000
SAVEHIST=1000000

# i have no idea what this stuff does! it was in here from the beginning
zstyle :compinstall filename "$ZDOTDIR/.zshrc"

autoload -Uz compinit
compinit

# futuristic zsh commands
autoload zmv

# completion stuff stolen from github.com/eevee/rc/.zshrc
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'r:|[._-]=**' 'r:|=**'
zstyle ':completion:*' menu select yes
zstyle ':completion:*:default' list-colors ''
zstyle ':completion:*' max-errors 2

# Turn on caching
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/"

# Show titles for completion types and group by type
zstyle ':completion:*:descriptions' format "$fg_bold[black]:: $fg_bold[blue]%d$reset_color"
zstyle ':completion:*' group-name ''

# Ignore some common useless files
zstyle ':completion:*' ignored-patterns '*?.pyc' '__pycache__' '.DS_Store' 'node_modules' 'package-lock.json'
zstyle ':completion:*:*:rm:*:*' ignored-patterns

# antigen
ADOTDIR=$ZDOTDIR/antigen
if [[ -a $ADOTDIR/antigen.zsh ]]; then
    source $ADOTDIR/antigen.zsh
    # redirects below so syntax highlighting doesn't whine about substring widgets being unhandled
    antigen bundle zsh-users/zsh-syntax-highlighting 2> /dev/null
    antigen bundle zsh-users/zsh-history-substring-search
    antigen apply
else
    mkdir -p $ADOTDIR
    git clone https://github.com/zsh-users/antigen $ADOTDIR
fi

# hopefully a much saner keyboard mapping section
bindkey -e
autoload zkbd
if [[ -a $ZDOTDIR/zkbd/$TERM ]]; then
    source $ZDOTDIR/zkbd/$TERM
    [[ -n ${key[Home]} ]] && bindkey "${key[Home]}" beginning-of-line
    [[ -n ${key[End]} ]] && bindkey "${key[End]}" end-of-line
    [[ -n ${key[Delete]} ]] && bindkey "${key[Delete]}" delete-char
    [[ -n ${key[Up]} ]] && bindkey "${key[Up]}" history-substring-search-up
    [[ -n ${key[Down]} ]] && bindkey "${key[Down]}" history-substring-search-down
fi
bindkey '^[[Z' reverse-menu-complete
bindkey '^N' history-substring-search-down
bindkey '^P' history-substring-search-up
bindkey '^W' vi-backward-kill-word
autoload edit-command-line
zle -N edit-command-line
bindkey '^X^E' edit-command-line

function star-history-search {
    eval 'LBUFFER=$(echo -n $(hs)) '
    zle reset-prompt
}
zle -N star-history-search
bindkey '^X^S' star-history-search
