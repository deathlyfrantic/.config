# shellcheck disable=SC2206
fpath+=(/opt/homebrew/share/zsh-completions/ /opt/homebrew/share/zsh/site-functions/ $XDG_CONFIG_HOME/zsh/completions)

# colors are important here in the future, where we live
autoload colors; colors

typeset -A fg_bright
if [[ $TERM == 'tmux-256color' || -n "$COLORTERM" || -n $VIMHOME || $TERM_PROGRAM == "Apple_Terminal" ]]; then
    fg_bright+=([black]=$(echo -e '\e[90m'))
    fg_bright+=([red]=$(echo -e '\e[91m'))
    fg_bright+=([green]=$(echo -e '\e[92m'))
    fg_bright+=([yellow]=$(echo -e '\e[93m'))
    fg_bright+=([blue]=$(echo -e '\e[94m'))
    fg_bright+=([magenta]=$(echo -e '\e[95m'))
    fg_bright+=([cyan]=$(echo -e '\e[96m'))
else
    # shellcheck disable=SC2296
    set -A fg_bright "${(@kv)fg_bold}"
fi

# general options
setopt appendhistory autocd extended_history share_history menu_complete prompt_subst
unsetopt beep case_glob flowcontrol

# prompt
set-up-prompt

# history
export HISTFILE="$XDG_DATA_HOME"/zsh-history
export HISTSIZE=1000000
export SAVEHIST=1000000

# i have no idea what this stuff does! it was in here from the beginning
zstyle :compinstall filename "$ZDOTDIR/.zshrc"

autoload -Uz compinit
compinit

# futuristic zsh commands
autoload zmv

# completion stuff stolen from https://github.com/eevee/rc/blob/master/.zshrc
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'r:|[._-]=**' 'r:|=**'
zstyle ':completion:*' menu select yes
zstyle ':completion:*:default' list-colors ''
zstyle ':completion:*' max-errors 2

# Turn on caching
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/"

# Show titles for completion types and group by type
zstyle ':completion:*:descriptions' format "${fg_bold[black]}:: ${fg_bold[blue]}%d$reset_color"
zstyle ':completion:*' group-name ''

# Ignore some common useless files
zstyle ':completion:*' ignored-patterns '*?.pyc' '__pycache__' '.DS_Store' 'node_modules' 'package-lock.json'
zstyle ':completion:*:*:rm:*:*' ignored-patterns

# substring search must be sourced after syntax highlighting to work correctly
for plugin in zsh-syntax-highlighting zsh-history-substring-search; do
    [ -f "/opt/homebrew/share/$plugin/$plugin.zsh" ] \
        && source "/opt/homebrew/share/$plugin/$plugin.zsh" \
        || echo "$plugin plugin not found"
done

# hopefully a much saner keyboard mapping section
bindkey -e
autoload zkbd
if [[ -a "$ZDOTDIR/zkbd/$TERM" ]]; then
    source "$ZDOTDIR/zkbd/$TERM"
    [[ -n ${key[Home]} ]] && bindkey "${key[Home]}" beginning-of-line
    [[ -n ${key[End]} ]] && bindkey "${key[End]}" end-of-line
    [[ -n ${key[Delete]} ]] && bindkey "${key[Delete]}" delete-char
    [[ -n ${key[Up]} ]] && bindkey "${key[Up]}" history-substring-search-up
    [[ -n ${key[Down]} ]] && bindkey "${key[Down]}" history-substring-search-down
fi
bindkey '^[[Z' reverse-menu-complete
bindkey '^N' history-substring-search-down
bindkey '^P' history-substring-search-up
bindkey '^W' vi-backward-kill-word
autoload edit-command-line
zle -N edit-command-line
bindkey '^X^E' edit-command-line

function star-history-search {
    eval 'LBUFFER=$(echo -n $(hs)) '
    zle reset-prompt
}
zle -N star-history-search
bindkey '^X^S' star-history-search
